!link -llfds

alias Entry::@Void
    func new(value=!Void)->Entry
        e := extern GC_malloc(24u64):Entry
        e.set_value(value)
        return e

    func get_value(e:Entry)->?Void
        return *(bitcast ((bitcast e as UInt)+16u64) as @?Void)

    func set_value(e:Entry, value:?Void)
        *(bitcast ((bitcast e as UInt)+16u64) as @?Void) = value

enum DequeueResult := Empty | Value(data:?Void)

alias Queue::@Void
    func new()->Queue
        q := extern GC_malloc(768u64):Queue
        dummy := Entry.new()
        extern lfds711_queue_umm_init_valid_on_current_logical_core(q, dummy, !Void)
        cleanup := extern lfds711_queue_umm_cleanup:(Queue,?Void)->Void
        extern GC_register_finalizer(q, cleanup, !Void, !Void, !Void)
        return q
    
    func enqueue(q:Queue, value:?Void)
        extern lfds711_queue_umm_enqueue(q, Entry.new(value))

    func dequeue(q:Queue)->?Void
        repeat if q.try_dequeue() is Value(v)
            return v
        else
            extern usleep(10u64)
        fail

    func try_dequeue(q:Queue)->DequeueResult
        entry := Entry.new()
        if extern lfds711_queue_umm_dequeue(q, &entry):Bool
            return Value(entry.get_value())
        else
            return Empty

new := Queue.new

if IS_MAIN_PROGRAM
    >>> q := Queue.new()
    say "Initialized!"
    for i in 10..15 do q.enqueue(@i)
    say "All queued up"
    >>> [repeat if q.try_dequeue() is Value(v) then (bitcast v as @Int) else stop]
