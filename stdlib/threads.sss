!link -lpthread

enum CreateResult := Success=0 | InsufficientResources=11 | InvalidSettings=22
enum JoinResult := Success(?Void) | NoPermissions=1 | Deadlock=35 | Invalid=22 | NotFound=3
alias Thread::Int
    func create(fn:(?Void)->?Void, arg:?Void)->Thread
        thread := Thread::0
        if status := extern GC_pthread_create(&thread, !Void, fn, arg):CreateResult is Success
            return thread
        else
            fail "Failed to create thread: $status"

    func join(t:Thread, _=!Void)->?Void
        ret := !Void
        if status := extern GC_pthread_join(t, &ret):JoinResult is Success
            return ret
        else
            fail "Failed to join thread: $status"

    func cancel(t:Thread)
        if extern GC_pthread_cancel(t):CError != CError.SUCCESS
            fail "Failed to cancel thread"

    self := extern pthread_self:()->Thread
        

create := Thread.create

if IS_MAIN_PROGRAM
    func test(v:?Void)->?Void
        i := *(bitcast (v or fail) as @Int)
        say "($i)"
        return @(10*i)

    >>> threads := [Thread.create(test, @i) for i in 1..10]
    >>> [*(bitcast t.join() as @Int) for t in threads]
    === [10, 20, 30, 40, 50, 60, 70, 80, 90, 100]
